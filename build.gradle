buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "$minecraft_version-$mod_version"

if (System.getenv().DRONE_BUILD_NUMBER != null) {
	version += "-snapshot-" + System.getenv().DRONE_BUILD_NUMBER
}

minecraft {
	version = "$minecraft_version-$forge_version"
	runDir = "run"

	replaceIn "shadowapi/ShadowAPIProps.java"
    replace '@VERSION@', "$mod_version"
}

repositories {
	maven {
		name = "shadowfacts"
		url = "http://maven.shadowfacts.net"
	}
}

dependencies {
	compile group: "net.shadowfacts", name: "ShadowLib", version: "1.3.0"
}

configurations {
	includedLibs
}

dependencies {
	includedLibs group: "net.shadowfacts", name: "ShadowLib", version: "1.3.0"
}

task fatJar(type: Jar) {
	classifier = 'fat'
	from { configurations.includedLibs.collect { it.isDirectory() ? it : zipTree(it) } }
	with jar
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	classifier = 'sources'
}

tasks.build.dependsOn fatJar, sourcesJar

artifacts {
	archives jar
	archives fatJar
	archives sourcesJar
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: "file:///maven/")
	}
}