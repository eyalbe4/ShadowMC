// Config
file("build.properties").withReader {
	def prop = new Properties()
	prop.load(it)
	project.ext.config = new ConfigSlurper().parse(prop)
}

// Buildscript
buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

// Plugins
apply plugin: "forge"
apply plugin: "maven"
apply plugin: "curseforge"
apply from: "gradle/scripts/dependencies.gradle"
apply from: "gradle/scripts/artifacts.gradle"

// Mod info
group = config.group
version = config.minecraft_version + "-" + config.mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = config.minecraft_version + "-" + config.forge_version
	runDir = "run"

	replaceIn "shadowmc/ShadowMC.java"
	replace '@VERSION@', config.mod_version

	srgExtra "PK: net/shadowfacts/shadowlib net/shadowfacts/shadowmc/shadowlib"
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", config.minecraft_version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':config.minecraft_version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}