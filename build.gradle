buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
		maven {
			name = "sonatype"
			url = "https://oss.sonatype.org/content/repositories/snapshots/"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
	}
}

apply plugin: 'forge'
apply plugin: 'maven'

version = "$minecraft_version-$mod_version"

if (System.getenv().BUILD_NUMER != null) {
	version += "-build-" + System.getenv().BUILD_NUMER
}

minecraft {
	version = "$minecraft_version-$forge_version"
	runDir = "run"

	replaceIn "shadowmc/ShadowMCProps.java"
    replace '@VERSION@', "$mod_version"

    srgExtra "PK: net/shadowfacts/shadowlib net/shadowfacts/shadowmc/shadowlib"
}

repositories {
	maven {
		name = "shadowfacts"
		url = "http://mvn.rx14.co.uk/shadowfacts/"
	}
}

configurations {
	shade
	compile.extendsFrom shade
}

dependencies {
	shade group: "net.shadowfacts", name: "ShadowLib", version: "1.3.0-build-9"
}

jar {
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
}

task deobfJar(type: Jar) {
	from sourceSets.main.output
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
	classifier = 'deobf'
}

task sourcesJar(type: Jar) {
	from sourceSets.main.allSource
	configurations.shade.each { dep ->
		from (project.zipTree(dep)) {
			exclude 'META-INF', 'META-INF/**'
		}
	}
	classifier = 'sources'
}

tasks.build.dependsOn deobfJar, sourcesJar

artifacts {
	archives jar
	archives deobfJar
	archives sourcesJar
}

uploadArchives {
	repositories.mavenDeployer {
		repository(url: "file:///maven/")
	}
}