buildscript {
	repositories {
		jcenter()
		maven {
			name = "forge"
			url = "http://files.minecraftforge.net/maven"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:2.1-SNAPSHOT'
	}
}

plugins {
	id "maven"
	id "com.github.johnrengelman.shadow" version "1.2.2"
	id "com.matthewprenger.cursegradle" version "1.0.5"
	id "org.ajoberstar.grgit" version "1.3.2"
}

apply plugin: "net.minecraftforge.gradle.forge"

import org.ajoberstar.grgit.*

ext.repo = Grgit.open(file("."))

version = minecraft_version + "-" + mod_version

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

minecraft {
	version = minecraft_version + "-" + forge_version
	runDir = "run"

	mappings = "snapshot_20151228"

	replaceIn "shadowmc/ShadowMC.java"
	replace '@VERSION@', mod_version
}

processResources {
	inputs.property "version", project.version
	inputs.property "mcversion", minecraft_version

	from (sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version':project.version, 'mcversion':minecraft_version
	}
	from (sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
	}
}

repositories {
	maven {
		name "shadowfacts"
		url "http://mvn.rx14.co.uk/shadowfacts/"
	}
}

dependencies {
	compile group: "net.shadowfacts", name: "ShadowLib", version: shadowlib_version
	
	compile group: "org.projectlombok", name: "lombok", version: "1.16.6"
}

shadowJar {
	classifier = ""
	dependencies {
		exclude(dependency("org.projectlombok:lombok:.*"))
	}
}

tasks.build.dependsOn sourceJar, shadowJar

artifacts {
	archives shadowJar
	archives sourceJar
}

task (release, dependsOn: ["generateChangelog", "tagRelease", "curseforge"])

task(generateChangelog) << {
	def changelog = ""
	def commits = repo.log {
		range(file("gradle/last-release-commit.txt").text, "HEAD")
	}
	commits.each {
		changelog += "- " + it.fullMessage
	}
	file("changelog.txt").text = changelog
	file("gradle/last-release-commit.txt").text = commits.first().id
}

task(tagRelease) << {
	repo.tag.add {
		name = version
		message = "Release ${version}"
	}
	repo.push(tags: true)
}

curseforge {
	apiKey = project.hasProperty('curseForgeApiKey') ? project.curseForgeApiKey : ''

	project {
		id = "226780"

		mainArtifact(shadowJar) {
			releaseType = "release"
			changelog = file("changelog.txt").text
		}
	}
}

tasks.curseforge.mustRunAfter generateChangelog